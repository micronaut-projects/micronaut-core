repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    api "info.picocli:picocli-groovy:$picocliVersion"
    api "org.apache.ant:ant:1.9.7"
    api dependencyVersion("jsr305")
    api dependencyModuleVersion("groovy", "groovy-ant")
    api dependencyModuleVersion("groovy", "groovy-json")
    api dependencyModuleVersion("groovy", "groovy-jmx")
    api dependencyModuleVersion("groovy", "groovy-xml")
    api dependencyModuleVersion("groovy", "groovy-templates")
    api "org.fusesource.jansi:jansi:1.14"
    api "jline:jline:2.12"
    api "org.springframework.boot:spring-boot-cli:2.2.5.RELEASE"
    api dependencyVersion("snakeyaml")
    runtimeOnly dependencyModuleVersion("slf4j", "slf4j-simple")
    runtimeOnly "org.codehaus.plexus:plexus-component-api:1.0-alpha-33"


    testImplementation "net.sf.expectit:expectit-core:0.9.0"
    testImplementation "com.github.jnr:jnr-posix:3.0.6"
}

File outDir = new File(project.buildDir, 'bin')

task createStartScripts(type: MicronautCreateStartScripts) {
    description = "Creates OS specific scripts to run the Micronaut CLI."
    mainClassName = 'io.micronaut.cli.MicronautCli'
    applicationName = 'mn'
    defaultJvmOpts = ["-XX:+TieredCompilation", "-XX:TieredStopAtLevel=1", "-XX:CICompilerCount=3"]
    outputDir = outDir
    classpath = configurations.runtime
    inputs.file 'src/main/resources/mn_completion'
    outputs.dir outDir
    doLast {
        ant.chmod(file: new File(outDir, 'mn'), perm: 'ugo+rx')
        copy {
            from 'src/main/resources/mn_completion'
            into outDir
        }
    }
}

class MicronautCreateStartScripts extends org.gradle.api.tasks.application.CreateStartScripts {
    Collection<String> projectArtifacts=[]

    @org.gradle.api.tasks.TaskAction
    void generate() {
        def generator = new org.gradle.api.internal.plugins.StartScriptGenerator()
        generator.unixStartScriptGenerator.template = project.resources.text.fromFile('src/main/resources/unixStartScript.txt')
        generator.applicationName = getApplicationName()
        generator.mainClassName = getMainClassName()
        generator.defaultJvmOpts = getDefaultJvmOpts()
        generator.optsEnvironmentVar = getOptsEnvironmentVar()
        generator.exitEnvironmentVar = getExitEnvironmentVar()
        generator.classpath = project.tasks.getByName('fatJar').outputs.files.collect { it.name }
        generator.scriptRelPath = "bin/${getUnixScript().name}"
        generator.generateUnixScript(getUnixScript())
        generator.generateWindowsScript(getWindowsScript())
    }
}

configurations.all {
    resolutionStrategy {
        // prevent fatJar result containing multiple versions of picocli
        force("info.picocli:picocli:$picocliVersion")
    }
}

task fatJar(type: Jar, dependsOn: ['createStartScripts']) {
    manifest {
        attributes 'Main-Class': 'io.micronaut.cli.MicronautCli',
                'Implementation-Version': project.version
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
    destinationDirectory = project.buildDir
}

task cliZip(type: Zip, dependsOn: ['fatJar']) {
    archiveFileName = "micronaut-${project.version}.zip"
    into("micronaut-${project.version}") {
        from('build/bin') {
            into('bin')
        }
        from('../media') {
            into('media')
        }
        from fatJar.outputs.files
        from('../LICENSE')
    }
}
