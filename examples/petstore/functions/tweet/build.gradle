import jp.classmethod.aws.gradle.lambda.*
import com.amazonaws.services.lambda.model.*

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        mavenLocal()

    }
    dependencies {
        classpath "jp.classmethod.aws:gradle-aws-plugin:0.22"

    }
}

plugins {
    id 'nebula.provided-base' version '3.0.3' // if you want provided-base
}

apply plugin:"groovy"
apply plugin: 'jp.classmethod.aws.lambda'
mainClassName = "example.function.tweet.UpdateStatusFunction"


dependencies {
    compileOnly "io.micronaut:inject-java:${projectVersion}"
    compile 'org.twitter4j:twitter4j-core:4.0.6'

    compile "io.micronaut:inject-groovy:${projectVersion}"
    compile "io.micronaut:function-groovy:${projectVersion}"


    runtime "com.amazonaws:aws-lambda-java-log4j2:1.0.0"
    runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.9.1'

    testCompile "io.micronaut:http-client:${projectVersion}"
    testRuntime "io.micronaut:discovery-client:${projectVersion}"
    testRuntime "io.micronaut:function-web:${projectVersion}"
    testRuntime "io.micronaut:http-server-netty:${projectVersion}"

    testCompile("org.spockframework:spock-core:1.1-groovy-2.4") {
        exclude module:'groovy-all'
    }

}

// NOTE: the AWS gradle plugin does not support altering environment variables so after
// deployment you have to go the console and set:
// - TWITTER_OAUTH_CONSUMER_KEY
// - TWITTER_OAUTH_CONSUMER_SECRET
// - TWITTER_OAUTH_ACCESS_TOKEN
// - TWITTER_OAUTH_ACCESS_TOKEN_SECRET
task deploy(type: AWSLambdaMigrateFunctionTask, dependsOn: shadowJar) {
    functionName = "twitter-update-status"
    handler = "example.function.tweet.UpdateStatusFunction::updateStatus"
    // ACTIVATE THIS FOR AWS Identity management
    //role = "arn:aws:iam::${aws.accountId}:role/lambda_basic_execution"
    runtime = Runtime.Java8
    zipFile = shadowJar.archivePath
    memorySize = 256
    timeout = 60
}

task invoke(type: AWSLambdaInvokeTask) {
    functionName = "twitter-update-status"
    invocationType = InvocationType.RequestResponse
    payload = '{"text":"Time is '+System.currentTimeMillis()+'"}'
    doLast {
        println "Lambda function result: " + new String(invokeResult.payload.array(), "UTF-8")
    }
}