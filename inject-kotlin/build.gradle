plugins {
    id "io.micronaut.build.internal.convention-library"
    id "org.jetbrains.kotlin.jvm"
    id "com.google.devtools.ksp" version "1.8.22-1.0.11"

}

micronautBuild {
    core {
        usesMicronautTest()
        usesMicronautTestKotest()
    }
}

dependencies {
    api project(":core-processor")

    implementation(libs.managed.ksp.api)
    if (!JavaVersion.current().isJava9Compatible()) {
        api files(org.gradle.internal.jvm.Jvm.current().toolsJar)
    }
    kspTest(project)
    kspTest platform(libs.test.boms.micronaut.validation)
    kspTest (libs.micronaut.validation.processor) {
        exclude group: 'io.micronaut'
    }

    testImplementation project(":jackson-databind")
    testImplementation project(":inject-kotlin-test")
    testImplementation libs.managed.kotlin.stdlib
    testImplementation project(':http-client')
    testImplementation libs.managed.jackson.annotations
    testImplementation libs.managed.reactor
    testImplementation libs.hibernate
    testImplementation platform(libs.test.boms.micronaut.validation)
    testImplementation (libs.micronaut.validation) {
        exclude group: 'io.micronaut'
    }
    testImplementation (libs.micronaut.validation.processor) {
        exclude group: 'io.micronaut'
    }
    testImplementation libs.javax.persistence
    testImplementation project(":runtime")
    testImplementation(libs.neo4j.bolt)
    testImplementation libs.managed.kotlinx.coroutines.core
    testImplementation libs.managed.kotlinx.coroutines.jdk8
    testImplementation libs.managed.kotlinx.coroutines.rx2
    testImplementation (libs.micronaut.test.junit5) {
        exclude group: 'io.micronaut'
    }
    testImplementation libs.kotlin.kotest.junit5
}

afterEvaluate {
    sourcesJar {
        from "$projectDir/src/main/kotlin"
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "17"
        freeCompilerArgs = ['-Xjvm-default=all']
    }
}

tasks.named("compileTestGroovy") {
    classpath += files(tasks.compileTestKotlin)
}

tasks.named("test") {
    classpath += files(tasks.compileTestKotlin)
//    testLogging {
//        showStandardStreams = true
//    }
    maxHeapSize("1G")
    forkEvery = 40
    maxParallelForks = 4
}

