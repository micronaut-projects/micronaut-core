plugins {
    id "io.micronaut.build.internal.common"
    id 'org.graalvm.buildtools.native'
}

micronautBuild {
    enableBom = false
    enableProcessing = false
}

repositories {
    mavenCentral()
    maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    annotationProcessor libs.bundles.asm
    annotationProcessor project(":inject-java")
    implementation project(":context")
    implementation project(":core")
    implementation project(":inject")
    implementation project(":graal")
    implementation project(":http-server-netty")
    implementation project(":http-client")
    implementation platform(libs.test.boms.micronaut.validation)
    implementation(libs.micronaut.validation) {
        exclude group: 'io.micronaut'
    }
    implementation project(":inject-java")

    testAnnotationProcessor libs.bundles.asm
    testAnnotationProcessor project(":inject-java")
    testImplementation (libs.micronaut.serde.jackson)
    testImplementation (libs.micronaut.test.junit5) {
        exclude group: 'io.micronaut'
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

configurations {
    all {
        // Stop serde pulling in AOP
        exclude group: 'io.micronaut', module: 'micronaut-aop'
        // Stop pulling in inject-groovy from maven
        exclude group: 'io.micronaut', module: 'micronaut-inject-groovy'
    }
    nativeImageTestClasspath {
        exclude module: 'groovy-test'
    }
}

tasks.named("check") { task ->
    def graal = ["jvmci.Compiler", "java.vendor.version", "java.vendor"].any {
        println "$it ${System.getProperty(it)}"
        System.getProperty(it)?.toLowerCase(Locale.ENGLISH)?.contains("graal")
    }
    if (graal) {
        task.dependsOn("nativeTest")
    }
}

def openGraalModules = [
        "org.graalvm.nativeimage.builder/com.oracle.svm.core.jdk",
        "org.graalvm.nativeimage.builder/com.oracle.svm.core.configure",
        "org.graalvm.sdk/org.graalvm.nativeimage.impl"
]

graalvmNative {
    toolchainDetection = false
    metadataRepository {
        enabled = true
    }
    binaries {
        all {
            openGraalModules.each { module ->
                jvmArgs.add("--add-exports=" + module + "=ALL-UNNAMED")
            }
        }
    }
}
