buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails:grails-docs:$grailsVersion"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'io.github.groovylang.groovydoc:groovydoc-gradle-plugin:1.0.1'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
    }
}

plugins {
    id 'com.github.hierynomus.license' version '0.14.0' apply false
}

repositories {
    maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://repo.grails.org/grails/core" }
}

version project.projectVersion

ext {
    distInstallDir = file("$buildDir/dist-tmp")
    homeBinDir = file("bin")
    homeLibDir = file("lib")
    homeSrcDir = file("src")
}

ext {
    dependencyVersions = [
        caffeine             : "com.github.ben-manes.caffeine:caffeine:$caffeineVersion",
        yaml                 : "org.yaml:snakeyaml:$yamlVersion",
        lombok               : "org.projectlombok:lombok:$lombokVersion",
        cglib                : "cglib:cglib:$cglibVersion",
        asm                  : "org.ow2.asm:asm:$asmVersion",
        asmCommons           : "org.ow2.asm:asm-commons:$asmVersion",
        asmTree              : "org.ow2.asm:asm-tree:$asmVersion",
        gormCore             : "org.grails:grails-datastore-core:$gormVersion",
        gormDatastore        : "org.grails:grails-datastore-gorm:$gormVersion",
        gormHibernate5       : "org.grails:grails-datastore-gorm-hibernate5:$gormVersion",
        grailsDocs           : "org.grails:grails-docs:$grailsVersion",
        groovy               : "org.codehaus.groovy:groovy:$groovyVersion",
        groovyTest           : "org.codehaus.groovy:groovy-test:$groovyVersion",
        groovyAnt            : "org.codehaus.groovy:groovy-ant:$groovyVersion",
        groovyJson           : "org.codehaus.groovy:groovy-json:$groovyVersion",
        groovyJmx            : "org.codehaus.groovy:groovy-jmx:$groovyVersion",
        jacksonDatabind      : "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
        jacksonAfterburner   : "com.fasterxml.jackson.module:jackson-module-afterburner:$jacksonVersion",
        jacksonDatatypeJdk8  : "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion",
        jacksonDatatypeJsr310: "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion",
        lettuce              : "io.lettuce:lettuce-core:$lettuceVersion",
        nettyHandler         : "io.netty:netty-handler:$nettyVersion",
        nettyHandlerProxy    : "io.netty:netty-handler-proxy:$nettyVersion",
        nettyCodecHttp       : "io.netty:netty-codec-http:$nettyVersion",
        reactor              : "io.projectreactor:reactor-core:$reactorVersion",
        rxJava2              : "io.reactivex.rxjava2:rxjava:$rxJava2Version",
        rxJavaInterop        : "com.github.akarnokd:rxjava2-interop:$rxJavaInteropVersion",
        rxJava1              : "io.reactivex:rxjava:$rxJava1Version",
        validation           : "javax.validation:validation-api:$validationVersion",
        slf4jSimple          : "org.slf4j:slf4j-simple:$slf4jVersion",
        slf4jApi             : "org.slf4j:slf4j-api:$slf4jVersion",
        spock                : "org.spockframework:spock-core:$spockVersion",
        neo4j                : "org.neo4j.test:neo4j-harness:$neo4jVersion",
        spring               : "org.springframework:spring-core:$springVersion",
        springContext        : "org.springframework:spring-context:$springVersion",
        hystrixCore          : "com.netflix.hystrix:hystrix-core:$hystrixVersion",
        hystrixSerialization : "com.netflix.hystrix:hystrix-serialization:$hystrixVersion",
        mongoDriver          : "org.mongodb:mongo-java-driver:$mongoVersion",
        mongoDriverAsync     : "org.mongodb:mongodb-driver-async:$mongoVersion",
    ]
}

subprojects { subproject ->

    version project.projectVersion

    ext {
        userOrg = "grails"
        isGrailsPlugin = false
        isConfiguration = subproject.projectDir.parentFile.name == 'configurations'
        isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")
    }

    if (subproject.name == "configurations") {
        return
    }

    if(isConfiguration) {
        group "io.micronaut.configuration"
    }
    else {
        group projectGroupId
    }


    repositories {
        maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    
    if (subproject.name.startsWith("examples")) {
        // apply plugin:"org.grails.grails-web"
    }
    else {
        apply plugin:"groovy"
        apply plugin:"java"
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
//        compileJava.options.compilerArgs.add '-parameters'
//        compileTestJava.options.compilerArgs.add '-parameters'
        if(!subproject.name.contains('test-suite')) {
            apply from:"${rootProject.rootDir}/gradle/publishing.gradle"
            apply plugin: 'checkstyle'
            
            checkstyle {
                toolVersion = 8.2
                configFile = new File(project.rootDir, 'config/checkstyle/checkstyle.xml')
                ignoreFailures = false

                // TODO - We need to turn these down once
                // the current issues are addressed.  Setting
                // high thresholds for now to allow builds to pass
                // until we can prioritize addressing the relevant
                // issues.
                maxErrors = 9_999
                maxWarnings = 9_999
            }
        }
    }

    tasks.withType(Test) {
        testLogging {
            showStandardStreams = true
            exceptionFormat = 'full'
        }
    }

    configurations {
        shadowCompile
        shadowRuntime
        documentation
        all {
            resolutionStrategy.eachDependency { DependencyResolveDetails details ->
                String group = details.requested.group
                if(group == 'org.codehaus.groovy') {
                    details.useVersion(groovyVersion)
                }
                if(group == 'org.ow2.asm') {
                    details.useVersion(asmVersion)
                }
            }
        }
    }

    dependencies {
        if (subproject.name != "bom") {
            compile "org.slf4j:slf4j-api:$slf4jVersion"
        }
//        shadowCompile group:"org.slf4j", name:"slf4j-api", version:slf4jVersion

        compileOnly([group:'com.github.ben-manes.caffeine',name:'caffeine', version:caffeineVersion])
        testCompile([group:'com.github.ben-manes.caffeine',name:'caffeine', version:caffeineVersion])
        testCompile "org.codehaus.groovy:groovy:$groovyVersion"
        testCompile("org.spockframework:spock-core:${spockVersion}") {
            exclude module:'groovy-all'
        }
        testCompile "cglib:cglib-nodep:2.2.2"
        testCompile "org.objenesis:objenesis:1.4"

        testRuntime "ch.qos.logback:logback-classic:1.2.3"
        testCompile "org.codehaus.groovy:groovy-test:$groovyVersion"
        compileOnly "org.ow2.asm:asm:$asmVersion"
        compileOnly "org.ow2.asm:asm-commons:$asmVersion"

        testCompile "org.ow2.asm:asm:$asmVersion"
        testCompile "org.ow2.asm:asm-commons:$asmVersion"
    }

    task allDeps(type: DependencyReportTask) {}
}

apply from:"gradle/docs.gradle"

// IDEA 2017.2 made a breaking change with IDE build output vs gradle build output
// [see https://youtrack.jetbrains.com/issue/IDEA-175172]
// the problem here is the gradle build uses <module-dir>/build/classes/main
// while IDEA uses <module-dir>/out/production/..
// As a result, compiling ast or java-inject module and using the IDEA test runner fails because the
// generated bean classes for injection are in <module-dir>/build, not <module-dir>/out
allprojects {
    apply plugin: 'idea'
    idea {
        module {
            outputDir file('build/classes/java/main')
            testOutputDir file('build/classes/groovy/test')
        }
    }
    apply from: rootProject.file('gradle/license.gradle')
}