plugins {
    id "io.micronaut.build.internal.convention-test-library"
    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.kotlin.kapt"
}

micronautBuild {
    core {
        usesMicronautTestJunit()
        usesMicronautTestSpock()
        usesMicronautTestKotest()
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api libs.managed.kotlin.stdlib
    api libs.managed.kotlin.reflect
    api libs.managed.kotlinx.coroutines.core
    api libs.managed.kotlinx.coroutines.jdk8
    api libs.managed.kotlinx.coroutines.rx2
    api project(':http-server-netty')
    api project(':http-client')
    api project(':runtime')

    testImplementation project(":context")
    testImplementation libs.managed.kotlin.test
    testImplementation libs.managed.kotlinx.coroutines.core
    testImplementation libs.managed.kotlinx.coroutines.rx2
    testImplementation libs.managed.kotlinx.coroutines.slf4j
    testImplementation libs.managed.kotlinx.coroutines.reactor
    testImplementation libs.managed.kotlinx.coroutines.reactive

    // Adding these for now since micronaut-test isnt resolving correctly ... probably need to upgrade gradle there too
    testImplementation libs.junit.jupiter.api
    testImplementation libs.awaitility
    testImplementation platform(libs.test.boms.micronaut.validation)
    testImplementation (libs.micronaut.validation) {
        exclude group: 'io.micronaut'
    }
    testImplementation project(":management")
    testImplementation project(':inject-java')
    testImplementation project(":inject")
    testImplementation libs.jcache
    testImplementation project(":http-client")
    testImplementation project(":http-client-jdk")
    testImplementation (libs.micronaut.session) {
        exclude group: 'io.micronaut'
    }
    testImplementation project(":jackson-databind")
    testImplementation libs.managed.groovy.templates

    testImplementation project(":function-client")
    testImplementation project(":function-web")
    testImplementation libs.kotlin.kotest.junit5
    testImplementation libs.logbook.netty
    kaptTest project(':inject-java')
    kaptTest platform(libs.test.boms.micronaut.validation)
    kaptTest (libs.micronaut.validation.processor) {
        exclude group: 'io.micronaut'
    }

    testImplementation libs.javax.inject
    testRuntimeOnly libs.junit.jupiter.engine
    testRuntimeOnly(platform(libs.test.boms.micronaut.aws))
    testRuntimeOnly libs.aws.java.sdk.lambda
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_15)) {
        testImplementation libs.bcpkix
    }

    testImplementation libs.managed.reactor
}

configurations.testRuntimeClasspath {
    resolutionStrategy.eachDependency {
        if (it.requested.group == 'org.jetbrains.kotlin') {
            it.useVersion(libs.versions.managed.kotlin.asProvider().get())
        }
    }
}

tasks.named("compileTestKotlin") {
    kotlinOptions.jvmTarget = "17"
}

tasks.named("test") {
    useJUnitPlatform()
}
