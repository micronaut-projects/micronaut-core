plugins {
    id "io.micronaut.build.internal.convention-test-library"
    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.kotlin.kapt"
}

micronautBuild {
    core {
        usesMicronautTestJunit()
        usesMicronautTestSpock()
        usesMicronautTestKotest()
    }
}

dependencies {
    api libs.kotlin.stdlib
    api libs.kotlin.reflect
    api libs.kotlinx.coroutines.core
    api libs.kotlinx.coroutines.jdk8
    api libs.kotlinx.coroutines.rx2
    api project(':http-server-netty')
    api project(':http-client')
    api project(':runtime')

    testImplementation project(":context")
    testImplementation libs.kotlin.test
    testImplementation libs.kotlinx.coroutines.core
    testImplementation libs.kotlinx.coroutines.rx2
    testImplementation libs.kotlinx.coroutines.slf4j
    testImplementation libs.kotlinx.coroutines.reactor

    // Adding these for now since micronaut-test isnt resolving correctly ... probably need to upgrade gradle there too
    testImplementation libs.junit.jupiter.api

    testImplementation project(":validation")
    testImplementation project(":management")
    testImplementation project(':inject-java')
    testImplementation project(":inject")
    testImplementation libs.managed.jcache
    testImplementation project(':validation')
    testImplementation project(":http-client")
    testImplementation project(":session")
    testImplementation libs.managed.groovy.templates

    testImplementation project(":function-client")
    testImplementation project(":function-web")
    testImplementation libs.kotlin.kotest.junit5
    testImplementation libs.logbook.netty
    kaptTest project(':inject-java')
    kaptTest project(':validation')
    testImplementation libs.javax.inject
    testImplementation(platform(libs.boms.micronaut.tracing))
    testImplementation(libs.micronaut.tracing.zipkin) {
        exclude module: 'micronaut-bom'
        exclude module: 'micronaut-http-client'
        exclude module: 'micronaut-inject'
        exclude module: 'micronaut-runtime'
    }

    testRuntimeOnly libs.junit.jupiter.engine
    testRuntimeOnly(platform(libs.boms.micronaut.aws))
    testRuntimeOnly libs.aws.java.sdk.lambda
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_15)) {
        testImplementation libs.bcpkix
    }

    testImplementation libs.managed.reactor
}

configurations.testRuntimeClasspath {
    resolutionStrategy.eachDependency {
        if (it.requested.group == 'org.jetbrains.kotlin') {
            it.useVersion(libs.versions.managed.kotlin.asProvider().get())
        }
    }
}

tasks.named("compileTestKotlin") {
    kotlinOptions.jvmTarget = "1.8"
}

tasks.named("test") {
    useJUnitPlatform()
}
