apply plugin: 'com.github.johnrengelman.plugin-shadow'
shadowJar {
    dependencies {
        for(dep in project.configurations.shadowCompile.dependencies) {
            exclude(dep)
        }
        exclude(project(":inject"))
        exclude(project(":core"))

        exclude(dependency(dependencyVersion("slf4j")))
        exclude(dependency(group: 'javax.inject', name: 'javax.inject', version: '1'))
        exclude(dependency('org.ow2.asm:.*:.*'))
        exclude(dependency('org.reactivestreams:.*:.*'))
        exclude(dependency('com.google.code.findbugs:.*:.*'))
        exclude(dependency('com.github.ben-manes.caffeine:.*:.*'))
    }

    relocate "org.objectweb.asm", "io.micronaut.asm"
    relocate "com.github.benmanes.caffeine", "io.micronaut.caffeine"
}

tasks.withType(com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation) { t ->
    t.enabled = false
}

dependencies {
    shadowCompile project(":inject")
    compile project(":inject")
    compile project(":aop")

    compileOnly files(org.gradle.internal.jvm.Jvm.current().toolsJar)
    compileOnly dependencyVersion("validation")

    testCompile project(":aop")
    testCompile project(":runtime")
    testCompile dependencyVersion("rxjava2")
    testCompile "org.hibernate:hibernate-core:5.1.5.Final"
    testCompile 'org.hibernate:hibernate-validator:5.4.1.Final'
    testRuntime 'org.glassfish.web:el-impl:2.2.1-b05'
    testCompile "junit:junit:4.7"
    testCompile "com.google.testing.compile:compile-testing:0.12"
    testCompile 'org.mongodb:mongo-java-driver:3.7.1'
    testCompile 'org.neo4j.driver:neo4j-java-driver:1.4.5'
    testCompile dependencyModuleVersion("groovy", "groovy-json")
    testCompile files(org.gradle.internal.jvm.Jvm.current().toolsJar)
}


