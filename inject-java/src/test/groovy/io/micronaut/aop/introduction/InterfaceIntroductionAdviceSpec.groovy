/*
 * Copyright 2017-2019 original authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package io.micronaut.aop.introduction

import io.micronaut.annotation.processing.test.AbstractTypeElementSpec
import io.micronaut.aop.Intercepted
import io.micronaut.context.ApplicationContext
import io.micronaut.context.BeanContext
import io.micronaut.context.DefaultBeanContext
import jakarta.validation.Valid
import jakarta.validation.constraints.Min
import spock.lang.Unroll
/**
 * @author Graeme Rocher
 * @since 1.0
 */
class InterfaceIntroductionAdviceSpec extends AbstractTypeElementSpec {

    @Unroll
    void "test AOP method invocation @Named bean for method #method"() {
        given:
        ApplicationContext context = ApplicationContext.run()
        InterfaceIntroductionClass foo = context.getBean(InterfaceIntroductionClass)

        expect:
        foo instanceof Intercepted
        args.isEmpty() ? foo."$method"() : foo."$method"(*args) == result

        cleanup:
        context.close()

        where:
        method                 | args         | result
        'test'                 | ['test']     | "changed"                   // test for single string arg
        'test'                 | ['test', 10] | "changed"    // test for multiple args, one primitive
        'testGenericsFromType' | ['test', 10] | "changed"    // test for multiple args, one primitive
    }

    void "test injecting an introduction advice with generics"() {
        ApplicationContext context = ApplicationContext.run()

        when:
        context.getBean(InjectParentInterface)

        then:
        noExceptionThrown()

        cleanup:
        context.close()
    }

    void "test typeArgumentsMap are created for introduction advice"() {
        def definition = buildBeanDefinition("test.Test\$Intercepted", """
package test;

import java.util.List;
import io.micronaut.aop.introduction.ParentInterface;
import io.micronaut.aop.introduction.Stub;

@Stub
interface Test extends ParentInterface<List<String>> {
}
""")

        expect:
        !definition.getTypeArguments(ParentInterface).isEmpty()
    }

    void "test type argument annotation propagation"() {
        def definition = buildBeanDefinition("test.Test\$Intercepted", """
package test;

import java.util.List;
import io.micronaut.aop.introduction.DataCrudRepo;
import io.micronaut.aop.introduction.Stub;
import jakarta.validation.Valid;
import jakarta.validation.constraints.Min;

@Stub
interface Test extends DataCrudRepo<@Valid  String, @Min(5) Integer> {
}
""")

        expect:
        definition.getRequiredMethod("save", String).getArguments()[0].getAnnotationMetadata().hasAnnotation(Valid)
        definition.getRequiredMethod("findById", Integer).getArguments()[0].getAnnotationMetadata().hasAnnotation(Min)
        definition.getRequiredMethod("findById", Integer).getReturnType().getAnnotationMetadata().hasAnnotation(Valid)
    }
}
